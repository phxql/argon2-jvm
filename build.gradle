plugins {
    id 'org.graalvm.buildtools.native' version '0.10.5' apply false
    id 'org.sonarqube' version '3.0' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'org.graalvm.buildtools.native'

    group 'de.mkammerer'
    version '2.12'
    description 'Argon2 Binding for the JVM'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    compileJava {
        // The generated bytecode is for a 1.6 JVM
        options.release = 6
    }

    java {
        toolchain {
            // We need a JDK 11 to build argon2-jvm
            languageVersion.set(JavaLanguageVersion.of(11))
        }

        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.12.0'
        testImplementation 'org.assertj:assertj-core:3.27.3'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            libs(MavenPublication) {
                from components.java
                pom(this.&configurePom)
            }
        }

        repositories {
            maven {
                String releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                String snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = project.properties['sonatype.username']
                    password = project.properties['sonatype.password']
                }
            }
        }
    }

    signing {
        sign publishing.publications.libs
        required { gradle.taskGraph.hasTask('publish') }
    }

    graalvmNative {
        binaries {
            main {
                buildArgs.add('--allow-incomplete-classpath')
            }
        }
    }
}

private def configurePom(MavenPom pom) {
    pom.with {
        name = 'Argon2 JVM'
        description = 'Argon2 Binding for the JVM'
        url = 'https://github.com/phxql/argon2-jvm'
        packaging = 'jar'
        licenses {
            license {
                name = 'GNU Lesser General Public License, Version 3'
                url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
            }
        }
        developers {
            developer {
                id = 'phxql'
                name = 'Moritz Halbritter'
                email = 'halbritter.moritz@gmail.com'
                url = 'https://www.mkammerer.de/'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/phxql/argon2-jvm.git'
            developerConnection = 'scm:git:git://github.com/phxql/argon2-jvm.git'
            url = 'https://github.com/phxql/argon2-jvm'
        }
    }
}
