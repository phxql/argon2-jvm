apply plugin: 'java-library'
apply plugin: 'org.sonarqube'

sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java', 'src/main/java11']
        }
    }
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'de.mkammerer.argon2.nolibs',
                'Multi-Release': 'true'
        )
    }
    into('META-INF/versions/11') {
        from sourceSets.java11.output
        include('module-info.class')
    }
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.16.0'
}

compileJava11Java {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.release = 11
    options.compilerArgs.addAll([
            '--module-path', sourceSets.main.compileClasspath.asPath,
            '--module-version', project.version]);
}

sonarqube {
    properties {
        property 'sonar.exclusions', 'src/main/java/de/mkammerer/argon2/jna/*.java'
    }
}
